# Snakefile
# Load the configuration file

configfile: "config.yaml"

import os
import glob

# Get VCF files from pattern in config
if "vcf_pattern" not in config:
    raise ValueError(
        "vcf_pattern must be specified in config.yaml\n"
        "Add: vcf_pattern: 'path/to/*.vcf.gz'"
    )

vcf_pattern = config["vcf_pattern"]
VCF_FILES = glob.glob(vcf_pattern)

if not VCF_FILES:
    raise ValueError(
        f"No VCF files found matching pattern: {vcf_pattern}\n"
        "Check your vcf_pattern in config.yaml"
    )

# Get output directory, default to current directory
OUTPUT_DIR = config.get("output_dir", ".")

# Extract basenames from VCF files
VCF_BASENAMES = [
    os.path.basename(vcf).replace('.vcf.gz', '').replace('.vcf', '')
    for vcf in VCF_FILES
]

# Create mapping for input lookup
VCF_MAP = {
    os.path.basename(vcf).replace('.vcf.gz', '').replace('.vcf', ''): vcf
    for vcf in VCF_FILES
}

rule all:
    input:
        expand("{output_dir}/{vcf}.no_multiallelics.filtered.vcf.gz",
               output_dir=OUTPUT_DIR,
               vcf=VCF_BASENAMES)

# --- Rule 1: Normalize VCF using bcftools add chrom:pos:ref:alt as id in there---
rule normalize_vcf:
    input:
        vcf = lambda wc: VCF_MAP[wc.vcf]
    output:
        normalized_vcf = temp("{output_dir}/{vcf}.no_multiallelics.vcf.gz")
    container: config.get("singularity")
    shell:
        """
        bcftools norm -m - {input.vcf} -Ou \
            | bcftools annotate \
                --set-id '%CHROM:%POS:%REF:%ALT' \
            | bcftools view -Oz -o {output.normalized_vcf}
        """

# --- Rule 2: Calculate Frequency using plink2 ---
rule calculate_frequency:
    input:
        normalized_vcf = "{output_dir}/{vcf}.no_multiallelics.vcf.gz"
    output:
        freq_file = "{output_dir}/{vcf}.no_multiallelics.afreq"
    params:
        out_base = "{output_dir}/{vcf}.no_multiallelics"
    threads: 1
    container: config.get("singularity")
    shell:
        """
        plink2 --vcf {input.normalized_vcf} \
               --snps-only \
               --set-all-var-ids @:#:\$r:\$a \
               --freq \
               --out {params.out_base} \
               --threads {threads}
        """

#Rule 3: Create list of variants to keep in analysis
rule pick_variants:
    input:
        allele_freq_file = "{output_dir}/{vcf}.no_multiallelics.afreq"
    params:
        exclusion_list = config.get("exclusion_list", "_NA"),
        script_path = "./scripts/variant_selector.py",
        add_chr = config.get("add_chr", True)
    output:
        var_incl_list = "{output_dir}/{vcf}.no_multiallelics.var_incl.list"
    container: config.get("singularity")
    shell:
        """
        python {params.script_path} \
            {input.allele_freq_file} \
            {output.var_incl_list} \
            --add_chr {params.add_chr} --exclusion_list {params.exclusion_list}
        """

# --- Rule 4: Create final vcf file with var_incl.list
rule final_filtering:
    input:
        var_incl_list = "{output_dir}/{vcf}.no_multiallelics.var_incl.list",
        vcf_file = "{output_dir}/{vcf}.no_multiallelics.vcf.gz"
    output:
        filtered_vcf = "{output_dir}/{vcf}.no_multiallelics.filtered.vcf.gz"
    params:
        out_base = "{output_dir}/{vcf}.no_multiallelics.filtered"
    threads: 1
    container: config.get("singularity")
    shell:
        """
        plink2 --vcf {input.vcf_file} \
               --extract {input.var_incl_list} \
               --recode vcf bgz \
               --out {params.out_base}
        """
